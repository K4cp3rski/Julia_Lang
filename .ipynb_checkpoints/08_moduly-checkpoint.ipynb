{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "0084d67c",
   "metadata": {},
   "source": [
    "# Moduły\n",
    "\n",
    "Do tej pory używaliśmy gotowych modułów poprzez `using` lub `import`. Jak zrobić własny moduł?\n",
    "\n",
    "Moduły w Julii mają bardzo prostą strukturę\n",
    "\n",
    "```julia\n",
    "module Stuffs\n",
    "\n",
    "# kod\n",
    "\n",
    "struct Stuff\n",
    "#kod\n",
    "end\n",
    "\n",
    "\n",
    "end\n",
    "```\n",
    "\n",
    "zwykle kod modułu wrzucony do pojedynczego pliku byłby bardzo nieczytelny, więc jest rozbity na pliki zawierające pewne fragmenty\n",
    "\n",
    "```julia\n",
    "module Stuffs\n",
    "\n",
    "include(\"structs.jl\")\n",
    "include(\"functions.jl\")\n",
    "    #...\n",
    "end\n",
    "```\n",
    "\n",
    "Te funkcje i zmienne, które chcemy udostępnić w przypadku wywołania `using` muszą być wyeksportowane\n",
    "\n",
    "```julia\n",
    "module Stuffs\n",
    "\n",
    "export Stuff, movestuff\n",
    "\n",
    "struct Stuff\n",
    "    x::Float64\n",
    "end\n",
    "\n",
    "function movestuff(s, x)\n",
    "    if checkmove(s, x)\n",
    "        return s.x + x\n",
    "    else\n",
    "        return s.x\n",
    "    end\n",
    "end\n",
    "\n",
    "# Ta funkcja nie będzie wyekportowana\n",
    "function checkmove(s, x)\n",
    "    if s.x + x > 0\n",
    "        return true\n",
    "    else\n",
    "        return false\n",
    "    end\n",
    "end\n",
    "\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "23a51aa0",
   "metadata": {},
   "source": [
    "## Praca z projektem"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "18b5a6db",
   "metadata": {},
   "source": [
    "### Mały projekt\n",
    "\n",
    "Typowa praca z niewielkim, lokalnym projektem może składać się z \n",
    "1. Stworzenia modułu np. `A.jl`\n",
    "\n",
    "```julia\n",
    "\n",
    "module A\n",
    "\n",
    "export do_things\n",
    "\n",
    "function do_things()\n",
    "    print(\"working...\")\n",
    "    #...\n",
    "    println(\"done!\")\n",
    "end\n",
    "\n",
    "end\n",
    "```\n",
    "2. Stworzenia pliku z kodem testującym modułu np. `testA.jl`\n",
    "\n",
    "```julia\n",
    "module testA\n",
    "\n",
    "include(\"A.jl\")\n",
    "using .A\n",
    "\n",
    "A.do_things()\n",
    "#lub\n",
    "function runtests()\n",
    "    A.do_things()\n",
    "end\n",
    "\n",
    "end\n",
    "```\n",
    "Takie objęcie testującego kodu w moduł powoduje, że ewentualne zmienne, funkcje itp. nie są umieszczane w globalnym kontekście - kod jest czystszy i jest mniejsza szansa konfliktów nazw.\n",
    "\n",
    "3. Następnie pracujemy w REPL\n",
    "\n",
    "```\n",
    "julia> include(\"testA.jl\")\n",
    "julia> testA.runtests()\n",
    "julia> ...\n",
    "```\n",
    "\n",
    "4. Nowe pomysły i poprawki wprowadzamy do A.jl lub testA.jl (w zależności od potrzeb).\n",
    "5. Zmieniony kod ładujemy wywołując `include(\"testA.jl\")`\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fadbe021",
   "metadata": {},
   "source": [
    "## Revise\n",
    "\n",
    "Pakiet `Revise` ułatwia pracę z projektami śledząc zmiany w pakietach i na bieżąco je ładując. Dzięki temu unikamy konfliktów raz zdefiniowanych struktur, modułów, konieczności przeładowywania środowiska (i utraty skompilowanych funkcji) itp.\n",
    "\n",
    "Zwykle podczas pracy nad projektem, zanim zaczniemy cokolwiek innego ładujemy moduł `Revise`.\n",
    "```\n",
    "julia> using Revise\n",
    "```\n",
    "\n",
    "Możemy teraz pracować nad projektem, a wywołanie \n",
    "```\n",
    "julia> include(\"testA.jl\")\n",
    "```\n",
    "powinno uruchamiać nasz kod testujący z wszelkimi zmianami we wszystkich modułach itp."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0c9c0fa7",
   "metadata": {},
   "source": [
    "# Menadżer pakietów\n",
    "\n",
    "`Pkg` jest menadżerem pakietów w Julii, który po pierwsze instaluje i usuwa pakiety. Ale, co równie istnotnie, zarządza całym systemem niezależnych środowisk (`environments`) przypisanych do różnych projektów. Jeżeli nie używamy przez dłuższy czas jakiegoś projektu, lub dostajemy go od kogoś, zazwyczaj okazuje się, że pakiety są nie w tej wersji, która jest potrzebna lub ich już nie ma. Każdy projekt może zależeć od różnych pakietów i różnych ich wersji, środowiska służą do rozwiązywania tego problemu - mają swoje niezależne wewnętrzne zależności i wersje. Uruchomienie środowiska przywraca taki stan, jaki został przypisany danemu projektowi. Pozwala to też wprowadzać poprawki niezależnie od oficjalnych kanałów. Jeżeli dany pakiet ma jakiś krytyczny błąd i chcemy go poprawić, możemy to zrobić wewnątrz jego środowiska i używać, a poprawkę niezależnie od tego wysłać do autorów (gdy zostanie wprowadzona, można oczywiście globalnie aktualizować dany pakiet)."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1fb89938",
   "metadata": {},
   "source": [
    "## Podstawy\n",
    "\n",
    "Po przejściu w REPL w tryb menadżera `]` możemy dodawać, aktualizować i usuwać pakiety poleceniami\n",
    "```\n",
    "pkg> add Pakiet\n",
    "pkg> update Pakiet\n",
    "pkg> rm Pakiet\n",
    "```\n",
    "\n",
    "alternatywą jest \n",
    "```\n",
    "julia> using Pkg\n",
    "julia> Pkg.add(\"Pakiet\")\n",
    "julia> Pkg.update(\"Pakiet\")\n",
    "julia> Pkg.rm(\"Pakiet\")\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8322e83c",
   "metadata": {},
   "source": [
    "## Środowiska\n",
    "\n",
    "Po naciśnieciu `]` otrzymujemy prompt w postaci\n",
    "\n",
    "`(@v1.7) pkg>`\n",
    "\n",
    "Informacja (@v1.7) pokazuje nam aktywne środowisko (w tym przypadku globalne).\n",
    "\n",
    "Polecenie\n",
    "\n",
    "`pkg> status`\n",
    "\n",
    "powinno pokazać nam wersje pakietów w tym środowisku, np.\n",
    "```\n",
    "  [c7e460c6] ArgParse v1.1.4\n",
    "  [6e4b80f9] BenchmarkTools v1.3.1\n",
    "  [336ed68f] CSV v0.10.3\n",
    "  [5ae59095] Colors v0.12.8\n",
    "  [a93c6f00] DataFrames v1.3.2\n",
    "  [31c24e10] Distributions v0.25.52\n",
    "  [0337cf30] GRUtils v0.7.1\n",
    "  [f67ccb44] HDF5 v0.16.4\n",
    "...\n",
    "```\n",
    "\n",
    "Przechodzimy teraz do odpowiedniego miejsca, gdzie chcemy założyć projekt (np. ~/workspace/Project) i tworzymy nowe środowisko\n",
    "\n",
    "`pkg> activate Project`\n",
    "\n",
    "lub\n",
    "```\n",
    "using Pkg\n",
    "Pkg.activate(\"Project\")\n",
    "```\n",
    "\n",
    "Możemy sprawdzić teraz stan nowego projektu\n",
    "```\n",
    "pkg> status\n",
    "```\n",
    "(lub `Pkg.status()`, dalej polecenia będą już tylko w trybie menadżera)\n",
    "\n",
    "Jeżeli chcemy dodać zależności to będą one dodawane do projektu\n",
    "```\n",
    "pkg> add Test\n",
    "pkg> status\n",
    "```\n",
    "\n",
    "Jeżeli chcemy wrócić do naszego projektu lub czyjegoś (np. git clone) to możemy użyć środowiska do znalezienia się w odpowiednich wersjach wszystkich zależności\n",
    "```\n",
    "pkg> activate .\n",
    "pkg> instantiate\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "20d1cea1",
   "metadata": {},
   "source": [
    "## Nowe pakiety\n",
    "\n",
    "Tworzenie nowych pakietów w najprostszej wersji polega na wydaniu polecenia\n",
    "```\n",
    "pkg> generate Projekt\n",
    "```\n",
    "\n",
    "Aczkolwiek wygodniejsze i pełniejsze pakiety można tworzyć za pomocą `PkgTemplates`\n",
    "\n",
    "```julia\n",
    "using PkgTemplates\n",
    "\n",
    "t = Template(interactive=true)\n",
    "t(\"Projekt\")\n",
    "```\n",
    "wersja interaktywna pozwala ustalić jakie parametry chcemy zmienić w standardowym wzorcu. Zwykle będziemy chcieli podać nazwę użytkownika (zwykle github lub innego serwisu kontroli wersji) i ścieżkę lokalną (domyślna jest w JULIA_DIR/dev).\n",
    "\n",
    "```julia\n",
    "t = Template(;user=\"k.a.miernik@gmail.com\", dir=\"~/workspace\")\n",
    "t(\"Projekt\")\n",
    "```\n",
    "\n",
    "Jeżeli przejdziemy teraz do utworzonego projektu to możemy sprawdzić jego strukturę\n",
    "```\n",
    "shell>cd Projekt\n",
    "shell> tree .\n",
    ".\n",
    "├── LICENSE\n",
    "├── Manifest.toml\n",
    "├── Project.toml\n",
    "├── README.md\n",
    "├── src\n",
    "│   └── Projekt.jl\n",
    "└── test\n",
    "    └── runtests.jl\n",
    "\n",
    "```\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2d2149fd",
   "metadata": {},
   "source": [
    "Szablon zawiera pliki:\n",
    "* Project.toml - informacje o nazwie projektu, unikatowym numerze UUID, autorze i zależnościach\n",
    "* Manifest.toml - dokładny stan wszystkich zależności, wraz z Project.toml umożliwia dokładne odtworzenie środowiska\n",
    "* README.md, LICENSE\n",
    "* src/Projekt.jl - główny plik projektu:\n",
    "\n",
    "    ```julia\n",
    "        module Projekt\n",
    "\n",
    "        # Write your package code here.\n",
    "\n",
    "        end\n",
    "    ```\n",
    "* test/runtests.jl - plik z testami projektu\n",
    "\n",
    "    ```julia\n",
    "        using Projekt\n",
    "        using Test\n",
    "\n",
    "        @testset \"Projekt.jl\" begin\n",
    "            # Write your tests here.\n",
    "        end\n",
    "    ```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4fd2a242",
   "metadata": {},
   "source": [
    "Testy można uruchomić poprzez\n",
    "* `julia> include(\"test/runtests.jl\")`\n",
    "* `pkg> test`"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d272e80d",
   "metadata": {},
   "source": [
    "## Testy\n",
    "\n",
    "Projekty często składają się z bardzo wielu elementów powiązanych ze sobą w jawny i niejawny sposób. Zmiana funkcjonalności lub poprawki w jednej części mogą wywołać niezamierzone efekty w innej części projektu. Jedną z metod kontroli i pracy nad projektami jest technika tworzenia oprogramowania w oparciu o testy (Test driven development). Elementy tej metody można wykorzystać także i mniejszych projektach. Generalny schemat postępowania jest następujący:\n",
    "1. Tworzymy nowy test (np. funkcji, struktury, itp.), który przy znanych warunkach powinien podać znaną odpowiedź.\n",
    "2. Test początkowo powinien dawać wynik negatywny \n",
    "3. Piszemy odpowiednią funkcję w taki sposób, aby test dawał wynik pozytywny\n",
    "4. Wszystkie testy powinny teraz działać pozytywnie\n",
    "5. Nowy fragment kodu refaktoryzujemy: porządkujemy, rozbijamy na mniejsze funkcje, oczyszczamy z powtarzających się elementów, przenosimy w odpowiednie miejsce i dokumentujemy\n",
    "6. Testy nadal powinny działać poprawnie\n",
    "7. Powtarzamy od punktu 1 dla kolejnej funkcjonalności"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8b425663",
   "metadata": {},
   "source": [
    "Testy w Julii mają bardzo swobodną i elastyczną strukturę. Mogą być umieszczone w dowolnym miejscu, ale plik `test/runtest.jl` jest automatycznie tworzony przez generator pakietów i jest wygodnym miejscem. \n",
    "\n",
    "Test składa się z zestawów `@testset`, z których każdy składa się z testów jednostkowych `@test` sprawdzających czy wyrażenie zwraca prawdę czy fałsz. Wewnątrz środowiska `@testset` można swobodnie tworzyć zmienne, używać pętli itp. Testy również mogą być używane w pętlach, mogą być zagniedżone.\n",
    "\n",
    "Jeżeli testów jest dużo warto rozbić je na mniejsze pliki, które mogą być załączane w pliku `runtest.jl`\n",
    "\n",
    "Poniższy przykład testów pochodzi z projektu symulującego detektory promieniowania jonizującego. Jednym z elementów projektu są zagadnienia geometryczne: jaka jest odległość między punktami, obiektami, czy dany punkt znajduje się wewnątrz danego obiektu itd."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2c947f84",
   "metadata": {},
   "outputs": [],
   "source": [
    "# geometry_tests.jl\n",
    "\n",
    "@testset \"Distances\" begin\n",
    "    ax = Line([0.0, 0.0, 0.0], [1.0, 0.0, 0.0])\n",
    "    ay = Line([0.0, 0.0, 0.0], [0.0, 1.0, 0.0])\n",
    "    az = Line([0.0, 0.0, 0.0], [0.0, 0.0, 1.0])\n",
    "\n",
    "    lx = Line([0.0, 0.0, 1.0], [1.0, 0.0, 1.0])\n",
    "    ly = Line([0.0, 0.0, 1.0], [0.0, 1.0, 1.0])\n",
    "    lz = Line([0.0, 1.0, 0.0], [0.0, 1.0, 1.0])\n",
    "\n",
    "    for l1 in [ax, ay, az]\n",
    "        for l2 in [ax, ay, az]\n",
    "            @test distance(l1, l2) == 0.0\n",
    "        end\n",
    "        \n",
    "        for l2 in [lx, ly, lz]\n",
    "            @test distance(l1, l2) == 1.0\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "@testset \"Inside\" begin\n",
    "\n",
    "    s1 = Sphere([0.0, 0.0, 0.0], 1.0)\n",
    "\n",
    "    @test isin([0.0, 0.0, 0.0], s1) == true\n",
    "    @test isin([0.0, 0.0, 1.0], s1) == false\n",
    "    @test isin([1.0, 1.0, 1.0], s1) == false\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "64121484",
   "metadata": {},
   "outputs": [],
   "source": [
    "# runtest.jl\n",
    "using NucPhysSim\n",
    "using Test\n",
    "\n",
    "@testset \"All tests\" begin\n",
    "    include(\"geometry_tests.jl\")\n",
    "    include(\"ray_tests.jl\")\n",
    "    include(\"particle_tests.jl\")\n",
    "    include(\"physics_tests.jl\")\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "42760afb",
   "metadata": {},
   "source": [
    "## Zadanie\n",
    "\n",
    "Stworzyć szkic projektu za pomocą PkgTemplates. W katalogu test utworzyć plik z danymi wejściowymi \"sample.csv\"\n",
    "\n",
    "```\n",
    "x,y,z\n",
    "0.0,1.0,2.0\n",
    "0.0,2.0,1.0\n",
    "1.0,0.0,2.0\n",
    "1.0,2.0,0.0\n",
    "2.0,1.0,0.0\n",
    "2.0,0.0,1.0\n",
    "```\n",
    "\n",
    "Stworzyć testy:\n",
    "1. funkcji czytającej dane, który sprawdzi czy wczytane zostały wszystkie punkty (długość struktury DataFrame)\n",
    "2. funkcji wyliczających: sumę, średnią i odchylenia standardowego dla kolumn x, y i z\n",
    "    \n",
    "Następnie napisać odpowiednie funkcje, tak, aby wszystkie testy były zaliczone pozytywnie."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e402919a",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
